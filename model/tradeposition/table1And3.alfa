typedefs {
    isoLEI       = string("[A-Z0-9]{18,18}[0-9]{2,2}")
    isoCtyCode   = string("[A-Z]{3,3}")
    isoISIN      = string
    isoUPI       = string
    isoCFI       = string
}

/# Canonical ALFA representation of  - ESMA Regulatory Technical Standards (RTS) and  Implementing Technical Standards (ITS) in the Official Journal of the European 
   Union of 07.10.2022-TR depicting one authorative source of data elements across enterprise's multiple source systems. 
   All systems across the enterprise will use data as defined in this model to produce and consume data, allowing re-use of collected data across the enterprise. #/
trait emirRefit {

    /# Date and time of the submission of the report to the trade repository.#/
    reportingTimestamp : time
        
    /# In the case where the entity responsible for reporting has delegated the submission of the report to a third party or to the other counterparty, this entity 
       has to be identified in this field by a unique code. Otherwise the entity responsible for reporting should be identified in this field. #/
    reportSubmittingEntityId : isoLEI?
        
    /# Where a financial counterparty  is solely responsible, and legally liable, for reporting on behalf of both counterparties in accordance with Article 9(1)(a) of 
       Regulation (EU) No 648/2012 of the Parliament and of the Council and the non-financial counterparty does not decide to report itself the details of its OTC 
       derivative contracts with the financial counterparty, the unique code identifying that financial counterparty. Where a management company is responsible, and 
       legally liable, for reporting on behalf of an Undertaking for Collective Investment in Transferable Securities (UCITS) in accordance with Article 9(1)(b) of that 
       Regulation, the unique code identifying that  management company. Where an Alternative Investment Fund Manager (AIFM) is responsible, and legally liable, for 
       reporting on behalf of an Alternative Investment Fund (AIF) in accordance with Article 9(1)(c) of that Regulation, the unique code identifying that AIFM. Where 
       an authorised entity that is responsible for managing and acting on be #/
    entityResponsibleForReporting : isoLEI
        
    /# Reporting counterparty. Identifier of the counterparty to a derivative transaction who is fulfilling its reporting obligation via the report in question. In the 
       case of an allocated derivative transaction executed by a fund manager on behalf of a fund, the fund and not the fund manager is reported as the counterparty. #/
    counterparty1 : isoLEI
        
    /# Indicate if the counterparty 1 is a CCP, a financial, non-financial counterparty or other type of counterparty in accordance with point 5 of Article 1 or points 1, 
       8 and 9 of Article 2 of Regulation (EU) No 648/2012 of the European Parliament and of the Council. #/
    natureOfCounterparty1 : enum<F, N, C, O>
        
    /# Nature of the counterparty 1's company activities. If the counterparty 1 is a Financial Counterparty, this field shall contain all necessary codes included in 
       the Taxonomy for Financial Counterparties and applying to that Counterparty. If the counterparty 1 is a Non-Financial Counterparty, this field shall contain all 
       necessary codes included in the Taxonomy for Non-Financial Counterparties and applying to that Counterparty. Where more than one activity is reported, the codes 
       shall be populated in order of the relative importance of the corresponding activities. #/
    corporateSectorOfCounterparty1 : list<enum<INVF,CDTI,INUN,UCIT,ORPI,AIFD,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,T,U>>
        
    /# Information whether the counterparty 1 is above the clearing threshold referred to in Article 4(a)(3) or 10(3) of Regulation (EU) No 648/2012 at the moment when 
       the transaction was concluded. #/
    clearingThresholdOfCounterparty1 : boolean
        
    # Indicator of whether LEI was used to identify the Counterparty 2.
    counterparty2IdentifierType : boolean
        
    # Identifier of the second counterparty to a derivative transaction.
    counterparty2 : isoLEI 
        
    # In case the counterparty 2 is a natural person, the code of country of residence of that person.
    countryOfCounterparty2 : isoCtyCode
        
    /# Indicate if the counterparty  2 is a CCP, a financial, non-financial counterparty or other type of counterparty in accordance with point 5 of Article 1 or 
       points 1, 8 and 9 of Article 2 of Regulation (EU) No 648/2012 of the European Parliament and of the Council. #/
    natureOfCounterparty2 : enum<F, N, C, O>
        
    /# Nature of the counterparty 2's company activities. If the counterparty 2 is a Financial Counterparty, this field shall contain all necessary codes included in 
       the Taxonomy for Financial Counterparties and applying to that Counterparty. If the counterparty 2 is a Non-Financial Counterparty, this field shall contain all 
       necessary codes included in the Taxonomy for Non-Financial Counterparties and applying to that Counterparty. Where more than one activity is reported, the codes 
       shall be populated in order of the relative importance of the corresponding activities. #/
    corporateSectorOfCounterparty2 : list<enum<INVF,CDTI,INUN,UCIT,ORPI,AIFD,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,T,U>>
        
    /# Information whether the counterparty 2 is above the clearing threshold referred to in Article 4(a)(3) or 10(3) of Regulation (EU) No 648/2012 at the moment 
       when the transaction was concluded. #/
    clearingThresholdOfCounterparty2 : boolean
        
    # Indicator of whether the counterparty 2 has the reporting obligation under EMIR (irrespective of who is responsible and legally liable for its reporting).
    reportingObligationOfCounterparty2 : boolean
        
    # In the case a broker acts as intermediary for the counterparty 1 without becoming a counterparty himself, the counterparty 1 shall identify this broker by a unique code.
    brokerId : isoLEI 
        
    # Identifier of the clearing member through which a derivative transaction was cleared at a central counterparty.
    clearingMember : isoLEI 
        
    # Indicator of whether the counterparty 1 is the buyer or the seller as determined at the time of the transaction.
    direction : enum<BYER,SLLR>
        
    # Indicator of whether the counterparty 1 is the payer or the receiver of leg 1 as determined at the time of the of the conclusion of the derivative.
    directionOfLeg1 : enum<MAKE,TAKE >
        
    # Indicator of whether the counterparty 1 is the payer or the receiver of leg 2 as determined at the time of the conclusion of the derivative.
    directionOfLeg2 : enum<MAKE,TAKE >
        
    #  This field shall be populated only where the counterparty 1 is a non-financial counterparty, as referred to in Article 2 (9) of the Regulation (EU) No 648/2012.
    directlyLinkedToCommercialActivityOrTreasuryFinancing : boolean
        
    # Unique Trade Identifier as referred to in Article 7 of the [ITS].
    uti : string(1, 35)?
        
    # Where a derivative was executed on a trading venue, a number generated by the trading venue and unique to that execution.
    reportTrackingNumber : string("^[A-Z]{1,52}$")?
        
    # UTI assigned to the predecessor transaction that has given rise to the reported transaction due to a lifecycle event, in a one-to-one relation between transactions (e.g. in the case of a novation, when a transaction is terminated, and a new transaction is generated) or in a one-to-many relation between transactions (e.g. in clearing or if a transaction is split into several different transactions). This data element is not applicable when reporting many-to-one and many-to-many relations between transactions (e.g. in the case of a compression).
    priorUti : string("^[A-Z]{1,52}$")?
        
    # The UTI of the position in which a derivative is included. This field is applicable only for the reports related to the termination of a derivative due to its inclusion in a position.
    subsequentPositionUti : string("^[A-Z]{1,52}$")?
        
    # Identifier generated by the PTRR service provider or CCP providing the PTRR service in order to connect all derivatives entering  into a given PTRR event and resulting from that PTRR event.
    postTradeRiskReduction : string("^[A-Z]{1,52}$")?
        
    # Identifier (determined by the counterparty  1) in order to connect derivatives in the same package in accordance with Article 1(2)(a). A package may include reportable and non- reportable transactions."
    packageIdentifier : string("^[A-Z]{1,35}$")?
        
    # ISIN identifying the product if that product is admitted to trading or traded on a regulated market, MTF, OTF or systematic internaliser.
    iInternationalSecuritiesIdentificationNumber : isoISIN?
        
    # UPI identifying the product.
    uniqueProductIdentifier : isoUPI?
        
    # Classification of Financial Instrument (CFI) code pertaining to the instrument.
    productClassification : isoCFI?
        
    # Each reported contract shall be classified according to its type.
    contractType : enum<CFDS,FRAS,FUTR,FORW,OPTN,SPDB,SWAP,SWPT,OTHR>
        
    # Each reported contract shall be classified according to the asset class it is based on.
    assetClass : enum<COMM,CRDT,CURR,INTR>
}
